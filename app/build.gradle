plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

import org.jetbrains.kotlin.gradle.dsl.JvmTarget
kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

android {
    namespace 'com.henrykvdb.sttt'
    compileSdk = 36

    defaultConfig {
        applicationId 'com.henrykvdb.sttt'
        minSdk 23
        targetSdk 36
        versionCode 37
        versionName '2.3.6'
        // TODO figure out if indonesia locale is 'in' or 'id' after ISO change
        resourceConfigurations += ['en', 'it', 'de', 'pt', 'zh', 'nl', 'in', 'id', 'ru', 'ko', 'fr', 'es']
    }

    buildTypes {
        release {
            debuggable = false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', 'admob_banner_id', '"ca-app-pub-6464324284979557/5400944625"'
        }
        debug {
            resValue 'string', 'admob_banner_id', '"ca-app-pub-3940256099942544/6300978111"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        disable 'ContentDescription'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    // Note: monitor https://github.com/AppIntro/AppIntro/issues/1123 so appintro_fixed.xml can be removed
    implementation 'com.github.AppIntro:AppIntro:6.3.1'

    implementation files('libs/ULtimateTTTApi.jar')

    implementation 'androidx.core:core-ktx:1.17.0' // Next version requires API target 34
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.10')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.10.0"

    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    // Firebase
    implementation(platform('com.google.firebase:firebase-bom:34.6.0'))
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-appcheck'
    implementation 'com.google.firebase:firebase-auth'

    // Ads networks + mediation
    implementation 'com.google.android.gms:play-services-ads:24.8.0'
    implementation 'com.google.ads.mediation:facebook:6.21.0.0'

    // Billing
    implementation 'com.android.billingclient:billing:8.1.0'

    // GDPR/consent message
    implementation 'com.google.android.ump:user-messaging-platform:4.0.0'
}